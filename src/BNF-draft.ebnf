<program> ::= <statement>*

<statement> ::= <variable_declaration>
              | <assignment>
              | <index_assignment>
              | <branching>
              | <loop>
              | <function_declaration>
              | <function_call>
              | <return_statement>
              | <output_statement>
              | <semicolon_statement>

<variable_declaration> ::= "+" <identifier> "~" <type> ("=" <expression>)? ";"

<assignment> ::= "<-" <identifier> "=" <expression> ";"

<index_assignment> ::= <index_access> "=" <expression> ";"

<branching> ::= "?" "[" <expression> "]" "|" <block> "~" (":" "|" <block> "~")?

<loop> ::= "%" "[" <expression> "]" "|" <block> "~"

<function_declaration> ::= "@" <identifier> "::" <parameters> "->" <type> "|" <block> "~"

<function_call> ::= "<>" <identifier> "::" "(" <arguments> ")" ";"

<return_statement> ::= "@" <identifier> "<-" <expression> ";"

<output_statement> ::= "^^" <expression> ";"

<semicolon_statement> ::= <expression> ";"

<block> ::= <statement>* 

<expression> ::= <literal>
               | <identifier>
               | <binary_expression>
               | <unary_expression>
               | <object_literal>
               | <property_access>
               | <index_access>
               | <method_call>
               | <type_construction>
               | <function_call>

<binary_expression> ::= <expression> <binary_operator> <expression>

<unary_expression> ::= <unary_operator> <expression>

<object_literal> ::= "(" <object_property> ("," <object_property>)* ")"

<object_property> ::= <identifier> ":" <expression>

<property_access> ::= <identifier> "->" <identifier>  // Only readable properties

<index_access> ::= <identifier> "->" "(" <expression> ")"

<method_call> ::= <identifier> "->" <string_method> "(" <arguments> ")"

<type_construction> ::= "string" "(" <expression> ")"
                      | "array" "<" <type> ">" "(" <arguments> ")"

<string_method> ::= "substring" | "replace" | "toUpper" | "toLower"

<parameters> ::= "none"
               | "<" <parameter> ("," <parameter>)* ">"

<parameter> ::= <identifier> "~" <type>

<arguments> ::= <expression> ("," <expression>)*

<literal> ::= <number_literal>
            | <string_literal>
            | <boolean_literal>

<identifier> ::= [a-zA-Z_][a-zA-Z0-9_]*

<type> ::= "number" | "string" | "boolean" | "array<T>" | "object<T>" | "void" | <struct_type>

<struct_type> ::= "$" <identifier> "::" "{" <struct_field> (<struct_field>)* "}"

<struct_field> ::= <identifier> ":" <type> ";"

<binary_operator> ::= "+" | "-" | "*" | "/" | "==" | "!=" | "<" | ">" | "<=" | ">=" | "."

<unary_operator> ::= "-" | "!"

<number_literal> ::= [0-9]+(\.[0-9]+)?

<string_literal> ::= "\"" [^\"]* "\""

<boolean_literal> ::= "yes" | "no"
